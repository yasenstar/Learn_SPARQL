# filename: ex209.rq

PREFIX dm: <http://learningsparql.com/ns/demo#>
PREFIX d: <http://learningsparql.com/ns/data#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# SELECT *
# ASK
CONSTRUCT {
    ?prob29item dm:problem dm:prob29 .
    dm:prob29   rdfs:label  "Location value must be a URI." .

    ?prob32item dm:problem dm:prob32 .
    dm:prob32   rdfs:label  "Amount must be an integer. " .

    ?prob44item dm:problem dm:prob44 .
    dm:prob44   rdfs:label  "Expenditures over 100 require grade 5 approval."
}
FROM <ex198.ttl>
WHERE {
    {
        ?prob29item dm:location ?city .
        FILTER (!(isURI(?city)))
    }
    UNION
    {
        ?prob32item dm:amount ?amount .
        FILTER ((datatype(?amount)) != xsd:integer )
    }
    UNION
    {
        ?prob44item   dm:cost   ?cost ;
                dm:amount   ?amount .
        OPTIONAL {
            ?item   dm:approval ?approvingEmployee .
            ?approvingEmployee  dm:jobGrade ?grade .
        }
        BIND (?cost * ?amount AS ?totalCost) .
        FILTER (
            ( ?totalCost > 100 )
            &&
            (
                (!(bound(?grade))
                ||
                (?grade < 5)
                )
            )
        )
    }
    
}